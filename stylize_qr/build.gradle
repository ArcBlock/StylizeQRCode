plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'maven-publish'
  id 'signing'
}
group='io.arcblock.forge'

def getArtificatId = { ->
  return "stylize_qr"
}
def getVersionName = { ->
  return System.getenv('TAG_NAME') ?: "1.0.0"
}


def snapshotRepositoryUrl = hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
      : "https://oss.sonatype.org/content/repositories/snapshots/"

def repositoryUsername = project.findProperty("nexus.username") ?: hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""

def repositoryPassword = project.findProperty("nexus.pw") ?: hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""


android {
  compileSdk 31

  defaultConfig {
    minSdk 21
    targetSdk 31

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

dependencies {
  implementation 'com.google.zxing:core:3.3.3'
  implementation 'androidx.core:core-ktx:1.7.0'
  implementation 'androidx.appcompat:appcompat:1.4.1'
  implementation 'com.google.android.material:material:1.5.0'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier.set("sources")
}
signing {
  sign publishing.publications
}


publishing {
  repositories {
    //    maven {
    //      name = "GitHubPackages"
    //      url = uri("https://maven.pkg.github.com/ArcBlock/StylizeQRCode")
    //      credentials {
    //        username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
    //        password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
    //      }
    maven {
      url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
      credentials {
        username = repositoryUsername
        password = repositoryPassword
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId 'io.arcblock.forge'
      artifactId getArtificatId()
      version getVersionName()
      artifact("$buildDir/outputs/aar/${getArtificatId()}-release.aar")
      pom {
        name = artifactId
        description = "android sdk base on zxing to generate stylize QR code"
        url = 'https://github.com/ArcBlock/StylizeQRCode'
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'paper'
            name = 'paper'
            email = 'paper@arcblock.io'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/ArcBlock/StylizeQRCode.git'
          developerConnection = 'scm:git:ssh://git@github.com/ArcBlock/StylizeQRCode.git'
          url = 'https://github.com/ArcBlock/StylizeQRCode'
        }

      }
    }
  }
}